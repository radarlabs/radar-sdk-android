apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    radarVersion = '3.1.5'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    kotlinOptions {
        jvmTarget = '1.8'
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName radarVersion
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.1.0'
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation "org.robolectric:robolectric:4.3.1"
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

tasks.dokkaJavadoc.configure {
    outputDirectory.set(file("$buildDir/javadoc"))
}

task androidJavadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from "$buildDir/javadoc"
}

artifacts {
    archives androidSourcesJar, androidJavadocsJar
}

publishing {
    publications {
        sdkPublication(MavenPublication) {
            groupId 'io.radar'
            artifactId 'sdk'
            version radarVersion
            artifact "$buildDir/outputs/aar/${project.getName()}-release.aar"
            artifact androidSourcesJar
            artifact androidJavadocsJar

            pom {
                name = 'Radar Android SDK'
                description = 'Android SDK for Radar, the leading geofencing and location tracking platform'
                url = 'https://radar.io'

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                organization {
                    name = 'Radar Labs, Inc'
                    url = 'https://radar.io'
                }

                developers {
                    developer {
                        id = 'radarlabs'
                        name = 'Radar Labs, Inc.'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com:radarlabs/radar-sdk-android.git'
                    developerConnection = 'scm:git:ssh://github.com:radarlabs/radar-sdk-android.git'
                    url = 'https://github.com/radarlabs/radar-sdk-android'
                }
            }
        }
    }

    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
        }
    }
}

signing {
    sign publishing.publications.sdkPublication
}
